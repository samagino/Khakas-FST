Multichar_Symbols

%>              ! morpheme boundary

%<n%>           ! Имя Существительное | Noun
%<num%>         ! Имя Числительное    | Numeral
%<adj%>         ! Имя Приоагательное  | Adjective
%<adv%>         ! Наречие             | Adverb
%<prn%>         ! Местоимение         | Pronoun

%<sg%>          ! единственного число  | Singular
%<pl%>          ! множественного число | Plural

%<p1%>          ! First Person
%<p2%>          ! Second Person
%<p3%>          ! Third Person

%<px1sg%>      ! 1st person singular possessive
%<px2sg%>      ! 2nd person singular possessive
%<px3sg%>      ! 3rd person singular possessive
%<px1pl%>      ! 1st person plural possessive
%<px2pl%>      ! 2nd person plural possessive
%<px3pl%>      ! 3rd person plural possessive


!--------- Cases -----------------!
%<nom%>         ! основной падеж       | nominative case
%<acc%>         ! винительный падеж    | accusitive case
%<dat%>         ! дательный падеж      | dative case
%<loc%>         ! местный падеж        | locative case
%<ins%>         ! орундийный падеж     | instrumental case
%<abl%>         ! исходный падеж       | ablative case
%<gen%>         ! родительный падеж    | genitive case
%<all%>         ! направительный падеж | allative case
!----------------------------------!


!------------ Archiphonemes ---------------!
%{а%}           ! а е
%{ы%}           ! і ы 0
%{з%}           ! з 0
%{с%}           ! з с
%{л%}           ! л н т
%{т%}           ! н т
%{д%}           ! д т
%{d%}           ! д н т
%{н%}           ! н 0
%{х%}           ! ғ г х к 0
!------------------------------------------!

!-------------Markers--------------------!
%{px3%}         ! 3rd person possessive

%{acc%}         ! accusitive
%{loc%}         ! locative
%{all%}         ! allative
%{abl%}         ! ablative
%{dat%}         ! dative
!----------------------------------------!


LEXICON Root

Nouns      ;
Numerals   ;
!Adjectives ;
!Adverbs   ;
Pronouns   ;

LEXICON CASE

%<nom%>:                          # ; ! nominative
%<acc%>:%>%{т%}%{ы%}%{acc%}       # ; ! accusitive
%<gen%>:%>%{т%}%{ы%}ң             # ; ! genetive
%<loc%>:%>%{н%}%{д%}%{а%}%{loc%}  # ; ! locative
%<all%>:%>%{н%}%{с%}%{а%}р%{all%} # ; ! allative
%<abl%>:%>%{d%}%{а%}ң%{abl%}      # ; ! ablative
%<ins%>:%>н%{а%}ң                 # ; ! instrumental
%<dat%>:%>%{х%}%{а%}%{dat%}       # ; ! dative

LEXICON POSSESSIVE

! NOTE: online grammar says px3pl should be {л}{а}р{і}
!            unless it follows plural suffix, in which case it is {і}
!       print grammar says it should be {з}{і} (same as px3sg)
!       here I follow the rule from the print grammar

                              CASE ; ! ¬ possessive
%<px1sg%>:%>%{ы%}м            CASE ; ! 1st person singular possessive
%<px2sg%>:%>%{ы%}ң            CASE ; ! 2nd person singular possessive
%<px3sg%>:%>%{з%}%{ы%}%{px3%} CASE ; ! 3rd person singular possessive
%<px1pl%>:%>%{ы%}б%{ы%}с      CASE ; ! 1st person plural possessive
%<px2pl%>:%>%{ы%}ң%{а%}р      CASE ; ! 2nd person plural possessive
%<px3pl%>:%>%{з%}%{ы%}%{px3%} CASE ; ! 3rd person plural possessive

LEXICON PLURAL

%<sg%>:               POSSESSIVE ; ! ¬ plural
%<pl%>:%>%{л%}%{а%}р  POSSESSIVE ; ! plural

LEXICON N

%<n%>: PLURAL ;

LEXICON Nouns

пала:пала       N;          ! ребенок  | child
тӧге:тӧге       N;          ! бревно   | log
кӧл:кӧл         N;          ! озеро    | lake
ізік:ізік       N;          ! дверь    | door
ӧртек:ӧртек     N;          ! утка     | duck
мал:мал         N;          ! животное | animal
тӧң:тӧң         N;          ! пригорок | hillock
киме:киме       N;          ! лодка    | boat
мии:мии         N;          ! мозг     | brain
хуу:хуу         N;          ! лебедь   | swan

LEXICON Numerals
! this is unfinished
! need ordinal numbers, production of big numbers

пір%<num%>:пір          #;        ! один        | one
ікі%<num%>:ікі          #;        ! два         | two
ӱс%<num%>:ӱс            #;        ! три         | three
тӧрт%<num%>:тӧрт        #;        ! четыре      | four
пис%<num%>:пис          #;        ! пять        | five
алты%<num%>:алты        #;        ! шесть       | six
читі%<num%>:читі        #;        ! семь        | seven
сигіс%<num%>:сигіс      #;        ! восемь      | eight
тоғыс%<num%>:тоғыс      #;        ! девять      | nine

он%<num%>:он            #;        ! десять      | ten
чибіргі%<num%>:чибіргі  #;        ! двадцать    | twenty
отыс%<num%>:отыс        #;        ! тридцать    | thirty
хырых%<num%>:хырых      #;        ! сорок       | forty
иліг%<num%>:иліг        #;        ! пятьдесят   | fifty
алтон%<num%>:алтон      #;        ! шестьдесят  | sixty
читон%<num%>:читон      #;        ! семьдесят   | seventy
сигізон%<num%>:сигізон  #;        ! восемьдесят | eighty
тоғызон%<num%>:тоғызон  #;        ! девяносто   | ninety

чӱс%<num%>:чӱс          #;        ! сто         | (one)hundred

!LEXICON Adjectives


!LEXICON Adverbs

LEXICON Pronouns

мин%<prn%>%<sg%>%<p1%>%<nom%>:мин       #;      ! я          | I
минің%<prn%>%<sg%>%<p1%>%<gen%>:минің   #;
мағаа%<prn%>%<sg%>%<p1%>%<dat%>:мағаа   #;
мині%<prn%>%<sg%>%<p1%>%<acc%>:мині     #;
минде%<prn%>%<sg%>%<p1%>%<loc%>:минде   #;
миннең%<prn%>%<sg%>%<p1%>%<abl%>:миннең #;
минзер%<prn%>%<sg%>%<p1%>%<all%>:минзер #;
миннең%<prn%>%<sg%>%<p1%>%<ins%>:миннең #;

син%<prn%>%<sg%>%<p2%>%<nom%>:син       #;      ! ты         | you
синің%<prn%>%<sg%>%<p2%>%<gen%>:синің   #;
сағаа%<prn%>%<sg%>%<p2%>%<dat%>:сағаа   #;
сині%<prn%>%<sg%>%<p2%>%<acc%>:сині     #;
синде%<prn%>%<sg%>%<p2%>%<loc%>:синде   #;
синнең%<prn%>%<sg%>%<p2%>%<abl%>:синнең #;
синзер%<prn%>%<sg%>%<p2%>%<all%>:синзер #;
синнең%<prn%>%<sg%>%<p2%>%<ins%>:синнең #;

ол%<prn%>%<sg%>%<p3%>%<nom%>:ол           #;      ! он/она/оно | he/she/it
аның%<prn%>%<sg%>%<p3%>%<gen%>:аның       #;
ағаа%<prn%>%<sg%>%<p3%>%<dat%>:ағаа       #;
аны%<prn%>%<sg%>%<p3%>%<acc%>:аны         #;
анда%<prn%>%<sg%>%<p3%>%<loc%>:анда       #;
аннаң%<prn%>%<sg%>%<p3%>%<abl%>:аннаң     #;
аныңзар%<prn%>%<sg%>%<p3%>%<all%>:аныңзар #;
аннаң%<prn%>%<sg%>%<p3%>%<ins%>:аннаң     #;

піс%<prn%>%<pl%>%<p1%>%<nom%>:піс       #;      ! мы         | we
пістің%<prn%>%<pl%>%<p1%>%<gen%>:пістің #;
піске%<prn%>%<pl%>%<p1%>%<dat%>:піске   #;
пісті%<prn%>%<pl%>%<p1%>%<acc%>:пісті   #;
пісте%<prn%>%<pl%>%<p1%>%<loc%>:пісте   #;
пістең%<prn%>%<pl%>%<p1%>%<abl%>:пістең #;
піссер%<prn%>%<pl%>%<p1%>%<all%>:піссер #;
піснең%<prn%>%<pl%>%<p1%>%<ins%>:піснең #;

сірер%<prn%>%<pl%>%<p2%>%<nom%>:сірер       #;      ! вы         | y'all
сірернің%<prn%>%<pl%>%<p2%>%<gen%>:сірернің #;
сірерге%<prn%>%<pl%>%<p2%>%<dat%>:сірерге   #;
сірерні%<prn%>%<pl%>%<p2%>%<acc%>:сірерні   #;
сірерде%<prn%>%<pl%>%<p2%>%<loc%>:сірерде   #;
сірердең%<prn%>%<pl%>%<p2%>%<abl%>:сірердең #;
сірерзер%<prn%>%<pl%>%<p2%>%<all%>:сірерзер #;
сірернең%<prn%>%<pl%>%<p2%>%<ins%>:сірернең #;

олар%<prn%>%<pl%>%<p3%>%<nom%>:олар       #;      ! они        | they
оларның%<prn%>%<pl%>%<p3%>%<gen%>:оларның #;
оларға%<prn%>%<pl%>%<p3%>%<dat%>:оларға   #;
оларны%<prn%>%<pl%>%<p3%>%<acc%>:оларны   #;
оларда%<prn%>%<sg%>%<p3%>%<loc%>:оларда   #;
олардаң%<prn%>%<sg%>%<p3%>%<abl%>:олардаң #;
оларзар%<prn%>%<sg%>%<p3%>%<all%>:оларзар #;
оларнаң%<prn%>%<sg%>%<p3%>%<ins%>:оларнаң #;